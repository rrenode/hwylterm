##[
  ## bbansi

  use BB style markup to add color to strings using VT100 escape codes
]##

{.push raises: [].}

import std/[macros, os, sequtils, strformat, strscans, strutils, terminal]
import ./bbansi/[styles, colors, wrapwords]
export wrapwords

type
  BbMode* = enum
    On
    NoColor
    Off
    Markup

  ColorSystem = enum
    TrueColor
    EightBit
    Standard
    None

  Console = object
    mode*: BbMode
    colorSystem*: ColorSystem
    file*: File

proc checkColorSystem(): ColorSystem =
  let colorterm = getEnv("COLORTERM").strip().toLowerAscii()
  if colorterm in ["truecolor", "24bit"]:
    return TrueColor
  let term = getEnv("TERM", "").strip().toLowerAscii()
  let colors = term.split("-")[^1]
  return
    case colors
    of "kitty": EightBit
    of "256color": EightBit
    of "16color": Standard
    else: Standard

proc newConsole*(file: File = stdout): Console =
  result.file = file
  result.mode = checkColorSupport(file)
  result.colorSystem = checkColorSystem()

var hwylConsole* = newConsole()

proc setHwylConsoleFile*(file: File) =
  hwylConsole.file = file
  hwylConsole.mode = checkColorSupport(file)

proc setHwylConsole*(c: Console) =
  hwylConsole = c

func firstCapital(s: string): string =
  s.toLowerAscii().capitalizeAscii()
func normalizeStyle(style: string): string =
  style.replace("_", "").toLowerAscii().capitalizeAscii()
func isHex(s: string): bool =
  (s.startswith "#") and (s.len == 7)

func toCode(style: BbStyle): string =
  $ord(style)
func toCode(abbr: BbStyleAbbr): string =
  abbr.toStyle().toCode()
func toCode(color: ColorXterm): string =
  "38;5;" & $ord(color)
func toBgCode(color: ColorXterm): string =
  "48;5;" & $ord(color)
func toCode(c: ColorRgb): string =
  "38;2;" & $c
func toBgCode(c: ColorRgb): string =
  "48:2;" & $c
func toCode(c: Color256): string =
  "38;5;" & $c
func toBgCode(c: Color256): string =
  "48;5;" & $c

macro enumNames(a: typed): untyped =
  ## unexported macro copied from std/enumutils
  result = newNimNode(nnkBracket)
  for ai in a.getType[1][1 ..^ 1]:
    assert ai.kind == nnkSym
    result.add newLit ai.strVal

const ColorXTermNames = enumNames(ColorXterm).mapIt(firstCapital(it))
const BbStyleNames = enumNames(BbStyle).mapIt(firstCapital(it))
# const ColorDigitStrings = (1..255).toSeq().mapIt($it)

func get256Color(s: string): int =
  try:
    if scanf(s, "Color($i)", result):
      if result > 255:
        result = 0
  except:
    discard

func parseStyle(mode: BbMode, style: string): string =
  try:
    var style = normalizeStyle(style)

    if style in ["B", "I", "U"]:
      return parseEnum[BbStyleAbbr](style).toCode()
    elif style in BbStyleNames:
      return parseEnum[BbStyle](style).toCode()

    if not (mode == On):
      return

    if style in ColorXtermNames:
      return parseEnum[ColorXterm](style).toCode()
    elif style.isHex():
      return style.hexToRgb.toCode()
    elif "Color(" in style:
      if (let num = style.get256Color(); num > 0):
        return num.toCode()
    else:
      when defined(debugBB):
        debugEcho "unknown style: " & style
  except:
    discard

func parseBgStyle(mode: BbMode, style: string): string =
  try:
    var style = normalizeStyle(style)
    if style in ColorXtermNames:
      return parseEnum[ColorXTerm](style).toBgCode()
    elif style.isHex():
      return style.hexToRgb().toBgCode()
    elif "Color(" in style:
      if (let num = style.get256Color(); num > 0):
        return num.toBgCode()
    else:
      when defined(debugBB):
        debugEcho "unknown style: " & style
  except:
    discard

func toAnsiCode*(mode: BbMode, s: string): string =
  if mode == Off:
    return
  var
    codes: seq[string]
    styles: seq[string]
    bgStyle: string
  if " on " in s or s.startswith("on"):
    let fgBgSplit = s.rsplit("on", maxsplit = 1)
    styles = fgBgSplit[0].toLowerAscii().splitWhitespace()
    bgStyle = fgBgSplit[1].strip().toLowerAscii()
  else:
    styles = s.splitWhitespace()

  for style in styles:
    let code = parseStyle(mode, style)
    if code != "":
      codes.add code

  if mode == On and bgStyle != "":
    let code = parseBgStyle(mode, bgStyle)
    if code != "":
      codes.add code

  if codes.len > 0:
    result.add "\e["
    result.add codes.join ";"
    result.add "m"

  when defined(assertAnsi):
    assert result != "", "unknown code: `" & s & "`"

proc toAnsiCode*(c: Console, s: string): string {.inline.} =
  toAnsiCode(c.mode, s)

proc toAnsiCode*(s: string): string {.inline.} =
  toAnsiCode(hwylConsole.mode, s)

func stripAnsi*(s: string): string =
  ## remove all ansi escape codes from a string
  var i: int
  while i < s.len:
    if s[i] == '\e':
      inc i
      if i < s.len and s[i] == '[':
        inc i
        while i < s.len and not (s[i] in {'A' .. 'Z', 'a' .. 'z'}):
          inc i
      else:
        result.add s[i - 1]
    else:
      result.add s[i]
    inc i

type
  BbSpan* = object
    styles*: seq[string]
    slice: HSlice[int, int]

  BbString* = object
    plain*: string
    spans*: seq[BbSpan]

func bbMarkup*(s: string, style: string): string =
  if style == "":
    return s
  ## enclose a string in bbansi markup for the given style
  result.add "["
  result.add style
  result.add "]"
  result.add s
  result.add "[/"
  result.add style
  result.add "]"

func bbEscape*(s: string): string {.inline.} =
  s.replace("[", "[[").replace("\\", "\\\\")


func bbEscape*(s: BbString): BbString {.inline.} =
  ## noop
  s

func shift(s: BbSpan, i: Natural): BbSpan =
  result = s
  inc(result.slice.a, i)
  inc(result.slice.b, i)

# proc size(span: BbSpan): int =
#   span.slice[1] - span.slice[0]

# TODO: make sure we don't get non-existent spans?
template endSpan(bbs: var BbString) =
  if bbs.spans.len == 0:
    return

  if bbs.plain.len == bbs.spans[^1].slice.a:
    bbs.spans.delete(bbs.spans.len - 1)
  elif bbs.plain.len >= 1:
    if bbs.spans.len > 1 and bbs.spans[^2].styles == bbs.spans[^1].styles:
        bbs.spans.delete(bbs.spans.len - 1)

    bbs.spans[^1].slice.b = bbs.plain.len - 1

  # assert bbs.spans[^1].slice.a <= bbs.spans[^1].slice.b
  # I think this is covered by the first condition now?
  # if bbs.spans[^1].size == 0 and bbs.plain.len == 0:
  #   bbs.spans.delete(bbs.spans.len - 1)

proc newSpan(bbs: var BbString, styles: seq[string] = @[]) =
  bbs.spans.add BbSpan(styles: styles, slice: bbs.plain.len..0)

template resetSpan(bbs: var BbString) =
  bbs.endSpan
  bbs.newSpan

template closeLastStyle(bbs: var BbString) =
  bbs.endSpan
  var newStyle: seq[string]
  if bbs.spans.len > 0 and bbs.spans[^1].styles.len >= 1:
    newStyle = bbs.spans[^1].styles[0 ..^ 2] # drop the latest style
  bbs.newSpan newStyle

template addToSpan(bbs: var BbString, pattern: string) =
  let currStyl = bbs.spans[^1].styles
  bbs.endSpan
  bbs.newSpan currStyl & @[pattern]

template closeStyle(bbs: var BbString, pattern: string) =
  let style = pattern[1 ..^ 1].strip()
  if bbs.spans[^1].slice.b == bbs.plain.len:
    bbs.endSpan
    bbs.newSpan
  elif style in bbs.spans[^1].styles:
    bbs.endSpan
    if bbs.spans.len == 0:
      return
    let newStyle = bbs.spans[^1].styles.filterIt(it != style) # use sets instead?
    bbs.newSpan newStyle

template closeFinalSpan(bbs: var BbString) =
  if bbs.spans.len >= 1:
    if bbs.spans[^1].slice.a == bbs.plain.len:
      bbs.spans.delete(bbs.spans.len - 1)
    elif bbs.spans[^1].slice.b == 0:
      bbs.endSpan

func bbImpl(s: string): BbString =
  ## convert bbcode markup to ansi escape codes
  var
    pattern: string
    i = 0

  result.plain = newStringofCap(s.len)

  template next() =
    result.plain.add s[i]
    inc i

  template incPattern() =
    pattern.add s[i]
    inc i

  template resetPattern() =
    pattern = ""
    inc i

  if not s.startswith('[') or s.startswith("[["):
    result.spans.add BbSpan()

  while i < s.len:
    case s[i]
    of '\\':
      if i < s.len and (s[i + 1] == '[' or s[i + 1] == '\\'):
        inc i
      next
    of '[':
      if i < s.len and s[i + 1] == '[':
        inc i
        next
        continue
      inc i
      while i < s.len and s[i] != ']':
        incPattern
      pattern = pattern.strip()
      if result.spans.len > 0:
        if pattern == "/":
          result.closeLastStyle
        elif pattern == "reset":
          result.resetSpan
        elif pattern.startswith('/'):
          result.closeStyle pattern
        else:
          result.addToSpan pattern
      else:
        result.newSpan @[pattern]
      resetPattern
    else:
      next

  closeFinalSpan result

proc bb*(s: string): BbString =
  bbImpl(s)

func len*(bbs: BbString): int =
  bbs.plain.len

func toMarkup(b: BbString): string =
  for span in b.spans:
    result.add b.plain[span.slice].bbEscape().bbMarkup(
      span.styles.join(" ")
    )

func toString(bbs: Bbstring, mode: BbMode): string =
  if mode == Off:
    return bbs.plain
  elif mode == Markup:
    return bbs.toMarkup()

  for span in bbs.spans:
    var codes = ""
    if span.styles.len > 0:
      codes = toAnsiCode(mode, span.styles.join(" "))

    result.add codes
    result.add bbs.plain[span.slice]

    if codes != "":
      result.add toAnsiCode(mode, "reset")

func toString*(c: Console, s: BbString): string {.inline.} =
  toString(s, c.mode)

proc `$`*(s: BbString): string =
  toString(hwylConsole, s)

proc bb*(s: BBString, style: string): BbString =
  ## apply a style to an existing BbString
  ## note: current implementation preforms a roundtrip conversion to markup
  s.toString(Markup).bbMarkup(style).bb()

# proc bb*(s: static string): BbString  {.compileTime.}=
#   bbImpl(s)

proc bb*(s: string, style: string): BbString =
  bb(bbMarkup(s, style))

proc bb*(s: Bbstring | string, style: Color256): BbString =
  bb(s, $style)

# error in vmgen when trying to define both the
# runtime and compile time (aka static string versions below)?
# proc bb*(s: static string, style: Color256): BbString =
#   bb(s, $style)
# proc bb*(s: static string, style: static string): BBString {.compileTime.} =
#   bbImpl(bbMarkup(s, style))

# # BUG: using static causes error for vm
template bbfmt*(pattern: static string): BbString =
  bbImpl(fmt(pattern))

proc bb*(s: BbString): BbString =
  ## noop
  s

func `&`*(x: BbString, y: string): BbString =
  result = x
  result.plain &= y
  result.spans.add BbSpan(styles: @[], slice: x.plain.len..(result.plain.len - 1))

proc `&`*(x: string, y: BbString): BbString =
  result.plain = x & y.plain
  result.spans.add BbSpan(styles: @[], slice: 0..(x.len - 1))
  let i = x.len
  for span in y.spans:
    result.spans.add span.shift(i)

func slice(s: BbString, span: BbSpan): string {.inline.} =
  s.plain[span.slice]

func truncate*(s: Bbstring, len: Natural): Bbstring =
  if s.len < len:
    return s
  for span in s.spans:
    if span.slice.a >= len:
      break
    if span.slice.b >= len:
      var finalSpan = span
      finalSpan.slice.b = len - 1
      result.spans.add finalSpan
      result.plain.add s.slice(finalSpan)
      break
    result.spans.add span
    result.plain.add s.slice(span)

func wrapWords*(
  s: BbString,
  maxLineWidth = 80,
  splitLongWords = true,
  seps: set[char] = Whitespace,
  newLine = "\n"
): BbString =
  ## wrap a bbstring while preserving styling
  ##
  ## note: the current implementation uses a roundtrip conversion back to markup first for wrapping
  s.toString(Markup).wrapWordsBbMarkup(maxLineWidth, splitLongWords, seps, newLine).bb()

func `&`*(x: BbString, y: BbString): Bbstring =
  result.plain.add x.plain
  result.spans.add x.spans
  result.plain.add y.plain
  let i = x.plain.len
  for span in y.spans:
    result.spans.add shift(span, i)

func add*(x: var Bbstring, y: Bbstring) =
  let i = x.plain.len
  x.plain.add y.plain
  for span in y.spans:
    x.spans.add shift(span, i)

# TODO: squash "like" spans for efficiency?
func add*(x: var Bbstring, y: string) =
  let i = x.plain.len
  x.plain.add y
  x.spans.add BbSpan(styles: @[], slice: i..(i + y.len - 1))

proc join*(a: openArray[Bbstring], sep: BbString = bb""): BbString =
  if len(a) == 0: return bb""
  add result, a[0]
  for i in 1..high(a):
    add result, sep
    add result, a[i]

proc join*(a: openArray[Bbstring], sep: string): BbString =
  join(a, sep.bbEscape().bb())

func repeat*(s: BbString, count: Natural): BbString =
  for i in 0..count-1:
    result.add s

func align*(s: BbString, count: Natural, padding = ' '): Bbstring =
  if s.len < count:
    result = (padding.repeat(count - s.len)) & s
  else:
    result = s

func alignLeft*(s: BbString, count: Natural, padding = ' '): Bbstring =
  if s.len < count:
    result = s & (padding.repeat(count - s.len))
  else:
    result = s

template echo*(c: Console, args: varargs[untyped]) =
  for x in @[args]:
    c.file.write(c.toString(x))
  c.file.write('\n')
  c.file.flushFile

# `$` already uses the global Console..
proc hecho*(args: varargs[string, `$`]) {.raises: [IOError].} =
  ## hwylterm builtin echo
  for x in args:
    hwylconsole.file.write(x)
  hwylConsole.file.write('\n')
  hwylConsole.file.flushFile

func `[]`*[T, U: Ordinal](s: BbString, x: HSlice[T, U]): BbString =
  if x.a < 0 or x.b > s.len:
    raise newException(IndexDefect, "slice out of bounds: " & $x)

  result.plain = newStringofCap(x.b - x.a)
  for span in s.spans:
    # skip spans before first index
    if x.a > span.slice.b: continue
    let
      a = max(span.slice.a, x.a)
      b = min(span.slice.b, x.b)
    result.spans.add BbSpan(styles: span.styles, slice: (a-x.a)..(b-x.a))
    result.plain.add s.plain[a..b]
    if x.b <= span.slice.b: break

func substr(s: BbString, first, last: int): BbString =
  let
    first = max(first, 0)
    last = min(last, high(s.plain))
    L = max(last - first + 1, 0)
  if L > 0:
    result = s[first..last]

iterator splitLines*(s: BbString, keepEol = false): BbString =
  var first = 0
  var last = 0
  var eolpos = 0
  while true:
    while last < s.len and s.plain[last] notin {'\c', '\l'}: inc(last)

    eolpos = last
    if last < s.len:
      if s.plain[last] == '\l': inc(last)
      elif s.plain[last] == '\c':
        inc(last)
        if last < s.len and s.plain[last] == '\l': inc(last)

    yield substr(s, first ,if keepEol: last-1 else: eolpos-1)

    if eolpos == last:
      break
    first = last

func hconcat*(a, b: BbString, sep = bb"", padding = bb" "): BbString =
  ## horizontally concatenate two strings with padding
  let
    aSplit = a.splitLines().toSeq()
    bSplit = b.splitLines().toSeq()
    aLenMax = aSplit.mapIt(it.len).max()
    bLenMax = bSplit.mapIt(it.len).max()

  var lines: seq[BbString]
  for i in 0..<max(aSplit.len, bSplit.len):
    var line: BbString
    if i < aSplit.len:
      line.add aSplit[i].alignLeft(aLenMax)
    else:
      line.add padding.repeat(aLenMax)
    line.add sep
    if i < bSplit.len:
      line.add bSplit[i].alignLeft(bLenMax)
    else:
      line.add padding.repeat(bLenMax)
    lines.add line

  result = join(lines,"\n")

func hconcat*(a, b: BbString, sep: string, padding = " "): BbString =
  ## See also:
  ## * `func hconcat`_
  hconcat(a, b ,sep = sep.bbEscape().bb(), padding = padding.bbEscape().bb())

